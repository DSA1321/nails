&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ТипыКонтрагентов.Клиент");
	КонецЕсли;
	ИзменитьНаименование();
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ТипКонтрагентаПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если Объект.ТипКонтрагента <> ПредопределенноеЗначение("Перечисление.ТипыКонтрагентов.Клиент")
		И (Элементы.ДатаРожденияКлиента.Видимость = Истина И (ЗначениеЗаполнено(Объект.Наименование) ИЛИ ЗначениеЗаполнено(Объект.ДатаРожденияКлиента) ИЛИ ЗначениеЗаполнено(Объект.ПолКлиента))) Тогда
		МассивОчищаемыхРеквизитов.Добавить("ДатаРожденияКлиента");
		МассивОчищаемыхРеквизитов.Добавить("ПолКлиента");
		МассивОчищаемыхРеквизитов.Добавить("Наименование");
		ТекстВопроса = "После изменения типа контрагента, будут очищены следующие реквизиты:
		| - «Наименование» (Если поле было заполнено)
		| - «Дата рождения клиента» (Если поле было заполнено) 
		| - «Пол клиента» (Если поле было заполнено)
		| - Продолжить?"; 
	ИначеЕсли Объект.ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ТипыКонтрагентов.Клиент")
		И (ЗначениеЗаполнено(Объект.Договор) ИЛИ ЗначениеЗаполнено(Объект.Наименование)) Тогда
		МассивОчищаемыхРеквизитов.Добавить("Договор");
		МассивОчищаемыхРеквизитов.Добавить("Наименование");
		ТекстВопроса = "После изменения типа контрагента, будут очищены следующие реквизиты:
		| - «Наименование» (Если поле было заполнено)
		| - «Договор» (Если поле было заполнено) 
		| - Продолжить?";
	КонецЕсли;
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	Иначе
		ИзменитьНаименование();
		УстановитьВидимость();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого Реквизит ИЗ Параметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		УстановитьВидимость();
		ИзменитьНаименование();
		ТипКонтрагента = Объект.ТипКонтрагента;
	Иначе
		Если Элементы.ДатаРожденияКлиента.Видимость = Истина Тогда
			Объект.ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ТипыКонтрагентов.Клиент");
		Иначе
			Объект.ТипКонтрагента = Неопределено;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаименование()
	Если Объект.ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ТипыКонтрагентов.Клиент") Тогда
		Элементы.Наименование.Заголовок = "Ф.И.О. клиента";
	Иначе
		Элементы.Наименование.Заголовок = "Наименование";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	Если Объект.ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ТипыКонтрагентов.Клиент") Тогда
		Элементы.ДатаРожденияКлиента.Видимость = Истина;
		Элементы.ПолКлиента.Видимость = Истина;
		Элементы.Договор.Видимость = Ложь;
	Иначе
		Элементы.ДатаРожденияКлиента.Видимость = Ложь;
		Элементы.ПолКлиента.Видимость = Ложь;
		Элементы.Договор.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры
