&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВыдачаПодотчетнику")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВыдачаЗаработнойПлаты")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ОплатаПоставщику")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВозвратПокупателю"))
		И ЗначениеЗаполнено(Объект.Получатель) Тогда
		МассивОчищаемыхРеквизитов.Добавить("Получатель");
		МассивОчищаемыхРеквизитов.Добавить("Договор");
		ТекстВопроса = "При изменении реквизита ""Вид операции"" будут очищены следующие данные:
		| - Получатель (Если поле было доступно для заполнения)
		| - Договор (Если поле было доступно для заполнения)
		| - Продолжить?";
	КонецЕсли;
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииВидаОперации", ЭтаФорма, МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Внимание!");
	Иначе
		ВидОперации = Объект.ВидОперации;
		УстановитьВидимость();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииВидаОперации(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		УстановитьВидимость();
	Иначе
		Объект.ВидОперации = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	СтрокаТипПолучателя = "";
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВыдачаПодотчетнику") Тогда
		Элементы.Договор.Видимость = Ложь;
		СтрокаТипПолучателя = "СправочникСсылка.Сотрудники";
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВыдачаЗаработнойПлаты") Тогда
		Элементы.Договор.Видимость = Ложь;
		СтрокаТипПолучателя = "СправочникСсылка.Сотрудники";
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ОплатаПоставщику") Тогда
		Элементы.Договор.Видимость = Истина;
		СтрокаТипПолучателя = "СправочникСсылка.Контрагенты";
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВозвратПокупателю") Тогда
		Элементы.Договор.Видимость = Истина;
		СтрокаТипПолучателя = "СправочникСсылка.Контрагенты";
	КонецЕсли;
	Если ЗначениеЗаполнено(СтрокаТипПолучателя) Тогда
		Массив = Новый Массив();
		Массив.Добавить(Тип(СтрокаТипПолучателя));
		ОписаниеТипаПолучателя = Новый ОписаниеТипов(Массив);
		Элементы.Получатель.ОграничениеТипа = ОписаниеТипаПолучателя;
	КонецЕсли;
	Если Элементы.Договор.Видимость = Истина И ЗначениеЗаполнено(Объект.Получатель) Тогда
		Элементы.Договор.Видимость = ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Объект.Получатель);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьВидимостьДоговораВЗависимостиОтТипаКонтрагента(Получатель)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Получатель", Получатель);
	Запрос.Текст = "ВЫБРАТЬ
	|	Контрагенты.ТипКонтрагента КАК ТипКонтрагента,
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Получатель";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Если ЗначениеЗаполнено(Выборка.ТипКонтрагента)
		И Выборка.ТипКонтрагента = Перечисления.ТипыКонтрагентов.Клиент Тогда
		возврат Ложь;
	Иначе
		возврат Истина;
	КонецЕсли;
КонецФункции